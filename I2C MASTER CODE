#include <Wire.h>

#define SDA_PIN 21
#define SCL_PIN 22
#define SLAVE_ADDR 0x08
#define MAX_LEN 32

volatile bool hasMsg = false;

char queued[MAX_LEN] = {0};
char rx[MAX_LEN] = {0};

uint8_t seq = 0;

// For onRequest state machine
volatile bool sendHeaderNext = true;

void onReceive(int len) {
  if (len < 1) return;

  uint8_t n = Wire.read();
  if (n >= MAX_LEN) n = MAX_LEN - 1;

  int i = 0;
  while (Wire.available() && i < n) rx[i++] = (char)Wire.read();
  rx[i] = '\0';

  Serial.print("From master: ");
  Serial.println(rx);
}

void onRequest() {
  if (sendHeaderNext) {
    uint8_t n = hasMsg ? (uint8_t)strlen(queued) : 0;
    Wire.write(seq);   // SEQ
    Wire.write(n);     // LEN
    sendHeaderNext = false;
  } else {
    if (hasMsg) {
      Wire.write((uint8_t*)queued, strlen(queued));
      hasMsg = false;      // clear after sending payload once
      queued[0] = '\0';
    }
    sendHeaderNext = true;
  }
}

void setup() {
  Serial.begin(115200);
  Wire.begin(SLAVE_ADDR, SDA_PIN, SCL_PIN, 100000);
  Wire.onReceive(onReceive);
  Wire.onRequest(onRequest);
  Serial.println("SLAVE READY");
}

void loop() {
  if (Serial.available()) {
    int len = Serial.readBytesUntil('\n', queued, MAX_LEN - 1);
    queued[len] = '\0';

    if (len > 0) {
      hasMsg = true;
      seq++;  // NEW message id only when user types a new line

      Serial.print("Queued for master: ");
      Serial.println(queued);
    }
  }

  delay(20);
}
